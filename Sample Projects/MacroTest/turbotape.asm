;*A,10000
;end
;*B

SETLFS=$FFBA
SETNAM=$FFBD
SAVE  =$FFD8
GETIN =$FFE4
CHROUT=$FFD2
PPLAY =$E31B
PRECO =$E319
RAMON =$FF3F
ROMON =$FF3E
LOCK  =$7FC
TIM2  =$FF02
TINT  =$FF09
SYNC  =$16
PIO   =1

FFIRST=$60     ;2B
FLAST =FFIRST+2;2B
DATA  =FLAST+2
CNT   =DATA+1
CHKSUM=CNT+1
TEMP  =CHKSUM+1
TOP   =TEMP+1
T0    =TOP+1
T1    =T0+1
T2=T1+1
T3=T2+1

K=$4700-$220
WOBBLE=$4000
* = $4700

READ:   PHP
        SEI
        LDA #$B8
        STA $25C
        LDA $FF19
        PHA

        JSR PPLAY
LODHIG: LDX #1
RETRY:  LDA #0
        STA $FF19
        STA CHKSUM
        STA PIO
        LDY #255
SYNCT:
        JSR READB-K
        CMP #SYNC
        BEQ GOTSYN

        STX DATA
        JSR SKIPB-K
        BCC SYNCT
GOTSYN:
        JSR READB-K
        CMP #SYNC
        BEQ GOTSYN

        CMP FNLEN
        BNE RETRY

NAMCHK: INY
        CPY FNLEN
        BCS NMEND
        JSR READC-K
        CMP FNAM,Y
        BEQ NAMCHK
        BNE RETRY
NMEND:
        LDY #3
GADDR:  JSR READC-K
        STA FFIRST,Y
        DEY
        BPL GADDR

        JSR READC-K
        CMP CHKSUM
        BNE ERROR

        LDY FFIRST
        LDA #0
        STA FFIRST
        STA TOP

RBLOCK: LDA FFIRST+1
        CMP FLAST +1
        PHP
        BCC NOTYT
        LDA FLAST
        BEQ RDONE
        STA TOP
NOTYT:
        JSR READC-K
        STA (FFIRST),Y
        INY
        CPY TOP
        BNE NOTYT

        JSR READC-K
        CMP CHKSUM
        BNE ERROR

        INC FFIRST+1
        PLP
        BNE RBLOCK
        PHA;DUMMY

RDONE:  LDX #0
  !byte $2C
ERROR:  LDX #$80
        PLA;DUMMY OR STATUS
        LDA #$88
        STA PIO
        STA LOCK

        PLA
        STA $FF19
        PLP
        JMP (BEGIN)

READC:  LDA DATA
        EOR CHKSUM
        STA CHKSUM
READB:  LDA #$FE
        STA DATA
SKIPB:  STY TEMP
LODLOW: LDY #$38
READ0:  INC $FF19
        LDA #$10
READ1:  BIT PIO
        BNE READ1
        STY TIM2
        STX TIM2+1
READ2:  BIT PIO
        BEQ READ2
        LDA TIM2+1
        ASL A
        ROL DATA
        BCS READ0
        LDY TEMP
        LDA DATA
        RTS
LAST:

BEGIN   =$2F0
FNLEN   =$2F2
FNAM    =$2F3


JUMP    =$304

        * = WOBBLE

 LDA #$F1
 STA $FF15
 LDA #$EE
 STA $FF19
 JSR EDCLR
TURBO:  LDX #0
TUR1:   LDA MESG,X
 BEQ TUR2
 JSR $FFD2
 INX
 BNE TUR1
TUR2:   JSR $FFE4
 CMP #32
 BCC TUR2
 CMP #96
 BCS TUR2
 JSR $FFD2
 CMP #'1'
 BEQ GENHED
 CMP #'2'
 BEQ GENTAP
 CMP #'M'
 BEQ MONITO
 CMP #'E'
 BEQ EDIT
 CMP #'3'
 BEQ LOADDK
 CMP #'4'
 BEQ CONST
 LDA #157
 JSR $FFD2
 JMP TUR2

EDIT:   JSR EDITOR
 JMP TURBO

LOADDK:JSR DISKLD
 JMP TURBO
CONST: JSR CONSTT
 JMP TURBO
MONITO: JSR $8117
 BRK

GENHED: JSR WMESG1
 JSR WMESG2
 JSR WMESG5
 JSR REPLY
 BCS GENHED
 SEI
 LDX #0
GENMVE: LDA READ,X
 STA $220,X
 INX
 CPX #LAST-READ
 BCC GENMVE
 LDA #<$220
 STA $302
 LDA #>$220
 STA $303
 JSR HEADER
 CLI
 JMP TURBO

GENTAP: JSR WMESG2
 JSR WMESG3
 JSR WMESG4
 JSR REPLY
 BCS GENTAP
 JSR EDDWN
 JSR WRITE
 JMP TURBO

GETHEX: LDA #0
 STA T0
 STA T1
GETHEZ: JSR GETIN
 CMP #13
 BEQ GETEX
 CMP #'0'
 BCC GETHEZ
 CMP #'F'+1
 BCS GETHEZ
 CMP #'9'+1
 BCC GOTDIG
 CMP #'A'
 BCC GETHEZ

GOTDIG: JSR CHROUT
 PHA
 LDX #3
DIGS:   ASL T0
 ROL T1
 DEX
 BPL DIGS
 PLA

 SEC
 SBC #48
 CMP #10
 BCC DIGSHF
 SBC #7
DIGSHF: ORA T0
 STA T0
 JMP GETHEZ
GETEX:  RTS

GETNAM: LDA #0
 STA T0
GETNIM: JSR GETIN
 CMP #13
 BEQ GETNEX
 CMP #32
 BCC GETNIM
 CMP #96
 BCS GETNIM

 JSR CHROUT
 LDX T0
 STA NAME,X
 INC T0
 LDA T0
 CMP #16
 BCC GETNIM
GETNEX: RTS

REPLY:  LDX #0
REP1:   LDA CORREC,X
 BEQ REP2
 JSR CHROUT
 INX
 BNE REP1
REP2:   JSR GETIN
 CMP #'Y'
 BEQ YES
 CMP #'N'
 BNE REP2
 SEC
 RTS
YES:    CLC
 RTS

WMESG2: LDX #0
WM2:    LDA MESG2,X
 BEQ WM21
 JSR $FFD2
 INX
 BNE WM2
WM21:   JSR GETNAM
 LDX T0
 STX FNLEN
 BEQ WM22
WM23:   LDA NAME-1,X
 STA FNAM-1,X
 DEX
 BNE WM23
WM22:   RTS

WMESG3: LDX #0
WM3:    LDA MESG3,X
 BEQ WM31
 JSR $FFD2
 INX
 BNE WM3
WM31:   JSR GETHEX
 LDA T0
 STA FFIRST
 LDA T1
 STA FFIRST+1
 RTS

WMESG4: LDX #0
WM4:    LDA MESG4,X
 BEQ WM41
 JSR $FFD2
 INX
 BNE WM4
WM41:   JSR GETHEX
 LDA T0
 STA FLAST
 LDA T1
 STA FLAST+1
 RTS

WMESG5: LDX #0
WM5:    LDA MESG5,X
 BEQ WM51
 JSR $FFD2
 INX
 BNE WM5
WM51:   JSR GETHEX
 LDA T0
 STA BEGIN
 LDA T1
 STA BEGIN+1
 RTS

WMESG1: LDX #0
WM1:    LDA MESG1,X
 BEQ WM11
 JSR $FFD2
 INX
 BNE WM1
WM11:   JSR GETNAM
 LDX T0
 STX HEADL
 BEQ WM12
WM13:   LDA NAME-1,X
 STA HEADN-1,X
 DEX
 BNE WM13
WM12:   RTS

HEADER: LDA #1
 TAX
 TAY
 JSR SETLFS
 LDA HEADL
 LDX #<HEADN
 LDY #>HEADN
 JSR SETNAM
 LDA #<$220
 STA T0
 LDA #>$220
 STA T1
 LDA #T0
 LDX #<$304
 LDY #>$304
 JMP SAVE

HEADL: !byte 0
HEADN: !fill 16

WRITE:  PHP
        SEI
        LDA $FF19
        PHA

        JSR PRECO
        STA RAMON
        LDA #2
        STA PIO

        LDA #0
        STA CHKSUM
        STA $FF19
        LDA #4
        JSR GAIT

        LDX #16
WSYNC:  LDA #SYNC
        JSR WRITEB
        DEX
        BNE WSYNC

        LDA FNLEN
        JSR WRITEB
        LDA FNLEN
        BEQ NONAME

        LDY #0
WNAME:  LDA FNAM,Y
        JSR WRITEB
        INY
        CPY FNLEN
        BCC WNAME
NONAME:
        LDX #3
WADDR:  LDA FFIRST,X
        JSR WRITEB
        DEX
        BPL WADDR

        LDA CHKSUM
        JSR WRITEB

        LDY FFIRST
        LDA #0
        STA FFIRST
        STA TOP

BLOCK:  JSR DELL
        LDA FFIRST+1
        CMP FLAST +1
        PHP
        BCC NOTYET
        LDA FLAST
        BEQ WDONE
        STA TOP
NOTYET:
        LDA (FFIRST),Y
        JSR WRITEB
        INY
        CPY TOP
        BNE NOTYET

        LDA CHKSUM
        JSR WRITEB

        INC FFIRST+1
        PLP
        BNE BLOCK
        PHA

WDONE:  PLA
        LDA #$88
        STA PIO
        STA LOCK
        STA ROMON

        PLA
        STA $FF19
        PLP
        RTS

WRITEB: STA DATA
        EOR CHKSUM
        STA CHKSUM
        TYA
        PHA
        LDY #8
WRT1:   INC $FF19
        LDA #0
        STA PIO
        JSR DELL
        ASL DATA
        BCC ZERO
        JSR DELL
ZERO:   LDA #2
        STA PIO
        JSR DELL
        DEY
        BNE WRT1
        PLA
        TAY
        RTS

GAIT:   STA CNT
        LDX #0
        LDY #0
GAIT1:  DEY
        BNE GAIT1
        DEX
        BNE GAIT1
        DEC CNT
        BNE GAIT1
        RTS

DELL:
SAVLOW:
        LDA #$60
        STA TIM2
SAVHIG: LDA #0
        STA TIM2+1
        LDA #16
        STA TINT
DEL11:  BIT TINT
        BEQ DEL11
        STA TINT
        RTS

MESG: !byte 19,19,147,9,14,31,8,13,13

!text " TURBO.TAPE.MENU.....",13,13
!text "  1) GENERATE HEADER",13
!text "  2) TURBO SAVE RAM",13
!text "  3) DISK LOAD",13
!text "  4) ALTER CONSTANTS",13
!text "  E) EDIT SCREEN",13
!text "  M) EXIT TO MONITOR",13,13
!text " ENTER OPTION : ",0

MESG1: !byte 147,13,13

!text " ENTER HEADER FILE NAME:",13,32,0

MESG2: !byte 147,13,13

!text " ENTER TURBO FILE NAME:",13,32,0

MESG3: !byte 13,13

!text " ENTER START ADDRESS:",13,32,0

MESG4: !byte 13,13

!text " ENTER END ADDRESS:",13,32,0

MESG5: !byte 13,13

!text " ENTER RUN ADDRESS:",13,32,0
CORREC:!text 13,13," CORRECT? Y/N ",0
NAME:  
!fill 16


DISKLD:
 JSR $FBD8
 !text 147,13," ENTER DISK FILE NAME",13,13,' ',0
 JSR GETNAM
 LDA T0
 BNE DISCON
 RTS
DISCON: LDX #<NAME
 LDY #>NAME
 JSR SETNAM
 LDA #8
 TAX
 LDY #255
 JSR SETLFS
 LDA #0
 JMP $FFD5

OUTHEX:TXA
 lsr
 lsr
 lsr
 lsr
 JSR OUTNIB
 TXA
 AND #15
 JSR OUTNIB
 TYA
 lsr
 lsr
 lsr
 lsr
 JSR OUTNIB
 TYA
 AND #15
OUTNIB:
 ORA #$30
 CMP #$3A
 BCC OKNIB
 ADC #6
OKNIB: JSR $FFD2
 RTS

CONSTT: JSR $FBD8
 !text 147,13,"CURRENT SAVE CONSTANT=",0
 LDX SAVHIG+1
 LDY SAVLOW+1
 JSR OUTHEX
 JSR $FBD8
 !text 13,13,"CURRENT LOAD CONSTANT=",0
 LDX LODHIG+1
 LDY LODLOW+1
 JSR OUTHEX
 JSR $FBD8
 !text 13,13,13,"ENTER SAVE>",0
 JSR GETHEX
 LDA T0
 ORA T1
 BEQ TRY2
 LDY T1
 STY SAVHIG+1
 LDX T0
 STX SAVLOW+1
TRY2: JSR $FBD8
!text 13,13,"ENTER LOAD>",0
 JSR GETHEX
 LDA T0
 ORA T1
 BEQ TRY3
 LDX T1
 LDY T0
 STX LODHIG+1
 STY LODLOW+1
TRY3:RTS

EDITOR: JSR WEDMES
EDWA:   JSR GETIN
 CMP #13
 BNE EDWA

 LDA #9
 JSR CHROUT
 LDA #19
 JSR CHROUT
 JSR EDDWN
 JSR $D965
 JSR EDUP
 RTS

EDUP: LDA #0
 STA T0
 STA T2
 LDA #8
 STA T1
 LDA #$48
 STA T3
 LDY #0
 LDX #8

EDU1: LDA (T0),Y
 STA (T2),Y
 INY
 BNE EDU1
 INC T1
 INC T3
 DEX
 BNE EDU1
 RTS

EDDWN: LDA #0
 STA T0
 STA T2
 LDA #$08
 STA T1
 LDA #$48
 STA T3
 LDX #8
 LDY #0

EDD1:  LDA (T2),Y
 STA (T0),Y
 INY
 BNE EDD1
 INC T1
 INC T3
 DEX
 BNE EDD1
 RTS

EDCLR: LDA #0
 STA T0
 LDA #$48
 STA T1
 LDY #0
 LDX #8
 LDA #32

EDC1:  STA (T0),Y
 INY
 BNE EDC1
 INC T1
 DEX
 BNE EDC1
 RTS


WEDMES: LDA #<EDMES
 STA WED1+1
 LDA #>EDMES
 STA WED1+2

WED1:   LDA $FFFF
 BEQ WED2
 JSR $FFD2
 INC WED1+1
 BNE WED1
 INC WED1+2
 BNE WED1
WED2:   RTS


EDMES: !byte 147,13,13

!text "USE CURSOR KEYS TO MOVE AROUND SCREEN.",13
!text "PRESSING A KEY STORES ITS CODE AT THAT",13
!text "POSITION. CHANGE COLOURS THE NORMAL",13
!text "MANNER. RETURN WILL EXIT EDIT MODE.",13,13
!text "WHEN TURBO SAVING, THE EDIT SCREEN IS",13
!text "DOWNLOADED. TO SAVE USE START ADDRESS",13
!text "OF 800. NB. KEEP REC/PLAY PRESSED AFTER",13
!text "GENERATING THE HEADER FILE.",13,13
!text "PRESS RETURN TO CONTINUE.",0
